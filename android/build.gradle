// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = findProperty('android.buildToolsVersion') ?: '35.0.0'
        minSdkVersion = Integer.parseInt(findProperty('android.minSdkVersion') ?: '24')
        compileSdkVersion = Integer.parseInt(findProperty('android.compileSdkVersion') ?: '35')
        targetSdkVersion = Integer.parseInt(findProperty('android.targetSdkVersion') ?: '35')
        kotlinVersion = findProperty('android.kotlinVersion') ?: '1.9.25'
        // 16KBページサイズ対応のためGoogle Mobile Ads SDKを最新に強制
        googlePlayServicesAdsVersion = '23.6.0'
    }
    repositories {
        google()
        mavenCentral()
    }
    configurations.all {
        resolutionStrategy {
            force "com.facebook.react:react-android:0.76.9"
            // 16KBページサイズ対応のためGoogle Mobile Ads SDKを強制更新
            // force "com.google.android.gms:play-services-ads:23.6.0"
            // force "com.google.android.gms:play-services-ads-base:23.6.0"
            // force "com.google.android.gms:play-services-ads-lite:23.6.0"
        }
    }
    dependencies {
        classpath('com.android.tools.build:gradle:8.7.3')
        classpath('com.facebook.react:react-native-gradle-plugin')
        classpath('org.jetbrains.kotlin:kotlin-gradle-plugin')
    }
}

apply plugin: "com.facebook.react.rootproject"

allprojects {
    repositories {
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url(new File(['node', '--print', "require.resolve('react-native/package.json')"].execute(null, rootDir).text.trim(), '../android'))
        }
        maven {
            // Android JSC is installed from npm
            url(new File(['node', '--print', "require.resolve('jsc-android/package.json', { paths: [require.resolve('react-native/package.json')] })"].execute(null, rootDir).text.trim(), '../dist'))
        }

        google()
        mavenCentral()
        maven { url 'https://www.jitpack.io' }
    }
    
    // 16KBページサイズ対応のため全モジュールでGoogle Mobile Ads SDKを強制更新
    configurations.all {
        resolutionStrategy {
            force "com.google.android.gms:play-services-ads:23.6.0"
            force "com.google.android.gms:play-services-ads-base:23.6.0"
            force "com.google.android.gms:play-services-ads-lite:23.6.0"
            force "com.google.mlkit:barcode-scanning:17.3.0"
        }
    }
}

subprojects {
    project.plugins.withId('com.android.library') {
        project.android {
            ndkVersion = "27.1.12297006"
        }
    }
}